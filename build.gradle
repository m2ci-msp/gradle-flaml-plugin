plugins {
    id 'com.gradle.plugin-publish' version '1.2.1'
    id 'groovy'
    id 'io.github.m2ci-msp.praat-wrapper' version '0.7.0'
}

group 'org.m2ci.msp'
version '0.5.0-SNAPSHOT'
description 'Utility plugin to manage speech data following FLAC+YAML convention'
ext.url = 'https://github.com/m2ci-msp/gradle-flaml-plugin'
ext.isReleaseVersion = !version.endsWith('-SNAPSHOT')

pluginBundle {
    website = project.url
    vcsUrl = project.url
    description = project.description
    tags = ['speech data', 'phonetics']
}

gradlePlugin {
    plugins {
        flamlPlugin {
            id = 'org.m2ci.msp.flaml'
            implementationClass = 'org.m2ci.msp.flaml.FlamlPlugin'
            displayName = project.description
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'de.undercouch', name: 'gradle-download-task', version: '5.3.0'
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.33'
    implementation group: 'io.github.m2ci-msp', name: 'jtgt', version: '0.7.0'
    testImplementation group: 'org.testng', name: 'testng', version: '7.5'
}

task generateTestResources(type: Exec) {
    dependsOn praat
    def scriptFile = objects.fileProperty()
    def destDir = objects.directoryProperty()
    scriptFile.set file('src/test/praat/generateTestResources.praat')
    destDir.set layout.buildDirectory.dir('generatedTestResources')
    inputs.file scriptFile
    outputs.dir destDir
    commandLine praat.binary, '--run', scriptFile.get().asFile, destDir.get().asFile
}

processTestResources {
    from generateTestResources, {
        rename { "org/m2ci/msp/flaml/$it" }
    }
}

test {
    useTestNG()
    systemProperty 'java.io.tmpdir', temporaryDir
    testLogging {
        events 'passed', 'failed', 'skipped'
        exceptionFormat = 'full'
        info {
            showStandardStreams = true
        }
    }
}

publishing {
    publications {
        withType(MavenPublication) {
            pom {
                name = 'FLAML Plugin'
                description = project.description
                url = project.url
                inceptionYear = '2017'
                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0-standalone.html'
                    }
                }
                developers {
                    developer {
                        name = 'Ingmar Steiner'
                        url = 'https://github.com/psibre'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/m2ci-msp/gradle-flaml-plugin.git'
                    developerConnection = 'scm:git:git@github.com/m2ci-msp/gradle-flaml-plugin.git'
                    url = project.url
                }
            }
        }
    }
}
