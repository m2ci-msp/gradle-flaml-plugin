plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'org.m2ci.msp.praat-wrapper' version '0.5.1'
    id 'com.gradle.plugin-publish' version '0.9.10'
}

gradlePlugin {
    plugins {
        flamlPlugin {
            id = pluginId
            implementationClass = 'org.m2ci.msp.flaml.FlamlPlugin'
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'de.undercouch', name: 'gradle-download-task', version: '3.4.3'
    compile group: 'org.yaml', name: 'snakeyaml', version: '1.21'
    testCompile group: 'org.testng', name: 'testng', version: '6.14.3'
}

task generateTestResources(type: GenerateTestResources) {
    dependsOn praat
    scriptFile = file('src/test/praat/generateTestResources.praat')
    destDir = layout.buildDirectory.dir('generatedTestResources')
}

processTestResources {
    from generateTestResources
}

test {
    useTestNG()
}

pluginBundle {
    website = scmUrl
    vcsUrl = scmUrl
    description = description
    tags = ['speech data', 'phonetics']

    plugins {
        flamlPlugin {
            id = pluginId
            displayName = description
        }
    }
}

class GenerateTestResources extends DefaultTask {

    @InputFile
    RegularFileProperty scriptFile = newInputFile()

    @OutputDirectory
    DirectoryProperty destDir = newOutputDirectory()

    @TaskAction
    void generate() {
        project.exec {
            commandLine project.praat.binary, '--run', scriptFile.get().asFile, destDir.get().asFile
        }
    }
}
